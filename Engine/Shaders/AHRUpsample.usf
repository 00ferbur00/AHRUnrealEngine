// @RyanTorant
#include "Common.usf"
#include "DeferredShadingCommon.usf"
#include "AHRCommon.usf"

Texture2D<uint4> tGI;
Texture2D NormalTex;
SamplerState samLinear;
float size;
float zMax;

static const float g_epsilon = 0.00001;

float4 PS(float2 InUV : TEXCOORD0,float3 ScreenVector : TEXCOORD1) : SV_TARGET0
{
	float SceneDepth = CalcSceneDepth(InUV)*0.01f;
	//float3 WorldPosition = ScreenVector * SceneDepth + View.ViewOrigin.xyz;
	//float3 depth_hires = distanceSquared(SceneDepth,View.ViewOrigin.xyz);

	const float2 coords[9] =
	{
		InUV + View.ViewSizeAndSceneTexelSize.zw*float2(-size,size),
		InUV + View.ViewSizeAndSceneTexelSize.zw*float2(0.0f, size),
		InUV + View.ViewSizeAndSceneTexelSize.zw*float2(size, size),

		InUV + View.ViewSizeAndSceneTexelSize.zw*float2(-size, 0.0f),
		InUV + View.ViewSizeAndSceneTexelSize.zw*float2(0.0f, 0.0f),
		InUV + View.ViewSizeAndSceneTexelSize.zw*float2(size, 0.0f),

		InUV + View.ViewSizeAndSceneTexelSize.zw*float2(-size, -size),
		InUV + View.ViewSizeAndSceneTexelSize.zw*float2(0.0f, -size),
		InUV + View.ViewSizeAndSceneTexelSize.zw*float2(size, -size),
	};

	float4 gidata = 0;
	float weight_sum = 0;
	float3 normal = GetScreenSpaceData(InUV).GBuffer.WorldNormal;

	[unroll]
	for (int i = 0; i < 9; i++) 
	{
		float z = CalcSceneDepth(coords[i])*0.01f;
		float3 n = GetScreenSpaceData(coords[i]).GBuffer.WorldNormal;
		//float3 wp = ScreenVector * z + View.ViewOrigin.xyz;
		//float3 linearZ = distanceSquared(SceneDepth,View.ViewOrigin.xyz);


		float w = 1.0f / (g_epsilon + pow(abs(SceneDepth - z),20));
		w *= lerp(1.0f / (g_epsilon + 1 - pow(abs(dot(n,normal)),8)),1,0.1); // reduce a bit the normal influence

		gidata += tGI.SampleLevel(samLinear, coords[i],0)*w;
		weight_sum += w;
	}
	gidata /= weight_sum;


	return gidata;
}
float4 NormalDownsample(float2 InUV : TEXCOORD0,float3 ScreenVector : TEXCOORD1) : SV_TARGET0
{
	float2 scale = View.ViewSizeAndSceneTexelSize.zw*1.75;

	float3 n0 = GetScreenSpaceData(InUV).GBuffer.WorldNormal;
	float3 n1 = GetScreenSpaceData(InUV + float2(scale.x,0)).GBuffer.WorldNormal;
	float3 n2 = GetScreenSpaceData(InUV + float2(-scale.x,0)).GBuffer.WorldNormal;
	float3 n3 = GetScreenSpaceData(InUV + float2(0,scale.y)).GBuffer.WorldNormal;
	float3 n4 = GetScreenSpaceData(InUV + float2(0,-scale.y)).GBuffer.WorldNormal;

	float3 normal = (n0 + n1 + n2 + n3 + n4)/5;

	return float4(normal*0.5+0.5,0);
}

static const int FilterSize = 13;
static const float2 PixelOffsets [ FilterSize ] =
{
    { -6, 0 },
    { -5, 0 },
    { -4, 0 },
    { -3, 0 },
    { -2, 0 },
    { -1, 0 },
    {  0, 0 },
    {  1, 0 },
    {  2, 0 },
    {  3, 0 },
    {  4, 0 },
    {  5, 0 },
    {  6, 0 },
};

uint4 BlurH(float2 InUV : TEXCOORD0,float3 ScreenVector : TEXCOORD1) : SV_TARGET0
{
	float4x4 sph = 0;
	float wacc = 1;

	float z = CalcSceneDepth(InUV);
	uint2 res;
	tGI.GetDimensions(res.x,res.y);
	uint2 DTid = round(InUV*res);
	float2 scale = View.ViewSizeAndSceneTexelSize.zw*size;

	[unroll]
	for (int i = 0; i < FilterSize; i++) 
    {
		float pz = CalcSceneDepth(InUV + PixelOffsets[i].xy*scale);
		float w = 1.0f / (g_epsilon + pow(abs(z - pz),2.5));

		uint4 s = tGI[DTid.xy + PixelOffsets[i].xy];
		float4x4 ds = Decode4SPH(s);

		sph[0] += ds[0]*w;
		sph[1] += ds[1]*w;
		sph[2] += ds[2]*w;
		sph[3] += ds[3]*w;

		wacc += w;
	}

	sph[0] /= wacc;
	sph[1] /= wacc;
	sph[2] /= wacc;
	sph[3] /= wacc;

	return Encode4SPH(sph[0],sph[1],sph[2],sph[3]);
}

uint4 BlurV(float2 InUV : TEXCOORD0,float3 ScreenVector : TEXCOORD1) : SV_TARGET0
{
	float4x4 sph = 0;
	float wacc = 1;

	float z = CalcSceneDepth(InUV);
	uint2 res;
	tGI.GetDimensions(res.x,res.y);
	uint2 DTid = round(InUV*res);
	float2 scale = View.ViewSizeAndSceneTexelSize.zw;

	[unroll]
	for (int i = 0; i < FilterSize; i++) 
    {
		float pz = CalcSceneDepth(InUV + PixelOffsets[i].yx*scale);
		float w = 1.0f / (g_epsilon + pow(abs(z - pz),2.5));

		uint4 s = tGI[DTid.xy + PixelOffsets[i].yx];
		float4x4 ds = Decode4SPH(s);

		sph[0] += ds[0]*w;
		sph[1] += ds[1]*w;
		sph[2] += ds[2]*w;
		sph[3] += ds[3]*w;

		wacc += w;
	}

	sph[0] /= wacc;
	sph[1] /= wacc;
	sph[2] /= wacc;
	sph[3] /= wacc;

	return Encode4SPH(sph[0],sph[1],sph[2],sph[3]);
}

